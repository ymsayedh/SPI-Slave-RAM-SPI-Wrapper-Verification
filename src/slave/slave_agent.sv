package slave_agent_pkg;

    import uvm_pkg::*;
    `include "uvm_macros.svh"
    import slave_main_seq_pkg::*;
    import slave_reset_seq_pkg::*;
    import slave_monitor_pkg::*;
    import slave_driver::*;
    import slave_seqr_pkg::*;
    import slave_config_obj::*;
    import slave_seq_item_pkg::*;

    class slave_agent extends uvm_agent;
        
        `uvm_component_utils(slave_agent)
        slave_monitor mon;
        slave_driver driver;
        slave_seqr sqr;
        slave_config_reg cfg;
        uvm_analysis_port #(slave_seq_item) agt_ap;

        function new(input string name = "slave_agent", uvm_component parent = null);
            super.new(name, parent);
        endfunction
        
        function void build_phase(input uvm_phase phase);
            super.build_phase(phase);
            if(!uvm_config_db #(slave_config_reg)::get(this, "", "CFG_SLAVE", cfg)) begin
                `uvm_fatal("build_phase", "Unable to reterive configuration object")
            end
            if(cfg.is_active == UVM_ACTIVE) begin
                sqr = slave_seqr::type_id::create("sqr", this);
                driver = slave_driver::type_id::create("driver", this);
            end
            mon = slave_monitor::type_id::create("mon", this);
            agt_ap = new("agt_ap", this);
        endfunction
        
        function void connect_phase(input uvm_phase phase);
            super.connect_phase(phase);
            if(cfg.is_active == UVM_ACTIVE) begin
                driver.slave_vif = cfg.slave_vif;
                driver.seq_item_port.connect(sqr.seq_item_export);
            end
            mon.slave_vif = cfg.slave_vif;
            mon.mon_ap.connect(agt_ap); 
        endfunction
        

    endclass
    

endpackage
